plugins {
    id 'com.gradleup.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

fabricApi {
    configureDataGeneration()
}

loom {
    runs {
        datagen {
            server()
            vmArg("-Dfabric-api.datagen")
            vmArg("-Dfabric-api.datagen.output-dir=${file("src/main/generated")}")
            vmArg("-Dfabric-api.datagen.modid=mysticlib")
            vmArg("-Dporting_lib.datagen.existing_resources=${file("src/main/resources")}")
        }
    }
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"
    modImplementation "dev.architectury:architectury-fabric:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')

    implementation("com.fasterxml.jackson.dataformat:jackson-dataformat-toml:$rootProject.jackson_dataformat_toml_version")
    include("com.fasterxml.jackson.dataformat:jackson-dataformat-toml:$rootProject.jackson_dataformat_toml_version")
}

processResources {
    inputs.property 'mod_version', project.mod_version
    inputs.property 'minecraft_version', project.minecraft_version
    inputs.property 'fabric_loader_version', project.fabric_loader_version
    inputs.property 'architectury_api_version', project.architectury_api_version

    filesMatching('fabric.mod.json') {
        expand mod_version: inputs.properties.mod_version,
                minecraft_version: inputs.properties.minecraft_version,
                architectury_api_version: inputs.properties.architectury_api_version,
                fabric_loader_version: inputs.properties.fabric_loader_version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}
