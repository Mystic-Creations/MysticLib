plugins {
    id 'com.gradleup.shadow'
}

loom {
    forge {
        mixinConfig "mysticlib.mixins.json"
    }
}

def generatedResources = file("src/generated/resources")

sourceSets {
    main {
        resources.srcDir generatedResources
    }
}

loom {
    runs {
        data {
            data()
            programArgs "--all", "--mod", "mysticlib"
            programArgs "--output", generatedResources.absolutePath
            programArgs "--existing", file("src/main/resources").absolutePath
        }
    }
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    forge "net.minecraftforge:forge:$rootProject.forge_version"

    modImplementation "dev.architectury:architectury-forge:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionForge')

    modImplementation 'com.github.GroundbreakingMC:TOMLy:1.0.1'
    include 'com.github.GroundbreakingMC:TOMLy:1.0.1'
    modImplementation "com.github.groundbreakingmc:Fission:1.0.0"
    include "com.github.groundbreakingmc:Fission:1.0.0"
}

processResources {
    inputs.property 'mod_version', project.mod_version
    inputs.property 'minecraft_version', project.minecraft_version
    inputs.property 'forge_version_range', project.forge_version_range
    inputs.property 'architectury_api_version', project.architectury_api_version

    filesMatching('META-INF/mods.toml') {
        expand mod_version: inputs.properties.mod_version,
                forge_version_range: inputs.properties.forge_version_range,
                minecraft_version: inputs.properties.minecraft_version,
                architectury_api_version: inputs.properties.architectury_api_version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}
